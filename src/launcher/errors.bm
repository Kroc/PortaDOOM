'copyright (C) Kroc Camen 2018-2020, BSD 2-clause
'error.bm : some shared error handling stuff

'    ErrorScreen(title$)
'    ErrorExit()
'    ErrorFileNotFound(file$)

'clear the screen for writing an error message
'=============================================================================
SUB ErrorScreen(title$)
    'disable global hot-keys
    CALL keys_off
    'clear the screen entirely, turning it red
    VIEW PRINT
    CLS, RED
    
    'draw the title / status bars
    COLOR BLACK, UI_FORECOLOR
    LOCATE UI_SCREEN_HEIGHT, 1: PRINT SPACE$(UI_SCREEN_WIDTH);
    LOCATE 1, 1: PRINT SPACE$(UI_SCREEN_WIDTH)
    
    'set up the scrolling zone
    COLOR YELLOW, RED
    VIEW PRINT 2 TO UI_SCREEN_HEIGHT - 1
    CLS 2: PRINT ""
    
    'print the error title
    PRINT " " + title$
    PRINT " " + STRING$(UI_SCREEN_WIDTH - 2, ASC_BOX_DBL_H)
    
END SUB

'wait for the user to press any key to exit after an error
'=============================================================================
SUB ErrorExit()
    PRINT ""
    PRINT " Command:"
    PRINT " " + STRING$(UI_SCREEN_WIDTH - 2, ASC_BOX_H)
    PRINTWRAP_X 2, UI_SCREEN_WIDTH - 2, COMMAND$(0) + " " + COMMAND$
    
    VIEW PRINT
    LOCATE UI_SCREEN_HEIGHT, 1
    COLOR BLACK+BLINK, WHITE
    
    DIM w%
    LET w% = LEN("PRESS ANY KEY TO EXIT!")
    PRINT SPACE$((UI_SCREEN_WIDTH - w%) / 2) _
        + "PRESS ANY KEY TO EXIT!" _
        + SPACE$((UI_SCREEN_WIDTH - w%) / 2);
    
    SLEEP: SYSTEM 1
END SUB

'Display "File Not Found" error
'=============================================================================
SUB ErrorFileNotFound(file$)
    CALL ErrorScreen("ERROR: File Not Found")
    PRINTWRAP_X 2, UI_SCREEN_WIDTH - 2, _
        "The file " + CHR$(34) + file$ + CHR$(34) _
      + " could not be found."
    CALL ErrorExit: SYSTEM 1
END SUB

'copyright (C) Kroc Camen 2018, BSD 2-clause

'create a new Label control and define its properties
'=============================================================================
FUNCTION UI.Labels.Add ( _
    x AS _UNSIGNED _BYTE, _
    y AS _UNSIGNED _BYTE, _
    foreColor AS _UNSIGNED _BYTE, _
    caption AS STRING _
)
    DIM kind_id AS _UNSIGNED LONG
    LET kind_id = UBOUND(UI.Labels) + 1
    REDIM _PRESERVE UI.Labels(1 TO kind_id) AS UILabel
    
    STRGUI.Begin
    STRGUI.Add caption
    LET UI.Labels(kind_id).caption = UI.Strings.Add (STRGUI.Compile$)
    
    LET UI.Labels.Add = UI.Controls.Add ( _
        x, y, 0, 0, foreColor, 0, _
        UIControlKind_Label, kind_id _
    )
END FUNCTION

'=============================================================================
SUB UI.Labels.Print (controlID AS _UNSIGNED _BYTE)
    DIM x AS _UNSIGNED _BYTE
    DIM y AS _UNSIGNED _BYTE
    LET x = UI.Controls(controlID).x
    LET y = UI.Controls(controlID).y
    
    LOCATE y, x
    
    'labels begin by inheriting the background colour of where they're being
    'printed; if you want to change the back-colour use the formatting codes
    COLOR UI.Controls(controlID).foreColor, SCREEN(y, x, 1) \ 16
    
    DIM kind_id AS _UNSIGNED _BYTE
    LET kind_id = UI.Controls(controlID).kind_id
    
    BINSTR.Print UI.Strings(UI.Labels(kind_id).caption)
END SUB

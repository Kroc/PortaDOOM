'copyright (C) Kroc Camen 2018, BSD 2-clause

'process a comma-separated list of tags, adding them to the known tag list
'=============================================================================
SUB AddTags (tagList$)
    'validation: be careful of fixed-width strings
    LET tagList$ = RTRIM$(tagList$)
    
    ECHO "* AddTags('" + tagList$ + "')"
    
    'the tag list will be broken into individual tags,
    'these will hold the individual tag index and name
    DIM tag%, tag$
    
    'prime the iterator
    LET tag$ = SplitTags$(tagList$)
    
    DO WHILE tag$ <> ""
        'check if the tag already exists:
        IF FindTag%(tag$) <> 0 THEN GOTO NextTag
        
        'increase the number of known tags
        LET tag% = UBOUND(Tags) + 1
        REDIM _PRESERVE Tags(1 TO tag%) AS STRING * 16
        LET Tags(tag%) = tag$
        
        ECHO "- AddTag: #" + STRINT$(tag%) + " " + tag$
        
        NextTag:
        LET tag$ = SplitTags$("")
    LOOP
END SUB

'search through the tag list and return an index number
'=============================================================================
FUNCTION FindTag% (tag$)
    'validation: be aware of fixed-length strings
    LET tag$ = RTRIM$(tag$)
    
    FOR FindTag% = 1 TO UBOUND(Tags)
        IF RTRIM$(Tags(FindTag%)) = tag$ THEN EXIT FUNCTION
    NEXT
    
    'not found
    LET FindTag% = 0
END FUNCTION

'this function, once given a list of comma-separated tags will return one tag
'each time the function is called until none remain
'=============================================================================
FUNCTION SplitTags$ (tagList$)
    'validation: be careful of fixed-width strings
    LET tagList$ = RTRIM$(tagList$)
    
    'if a list is provided, keep it for each additional call to the function
    STATIC list$
    IF tagList$ <> "" THEN LET list$ = tagList$
    
    'if no tags remain, then return nothing
    IF list$ = "" THEN EXIT FUNCTION
    
    'find the next comma
    DIM i%
    LET i% = INSTR(1, list$, ",")
    
    'if there are no commas in the tag-list, it's just one tag
    IF i% = 0 THEN
        'return the final tag
        LET SplitTags$ = list$
        'clear the remaining list
        LET list$ = ""
    ELSE
        'strip off the current tag
        LET SplitTags$ = LEFT$(list$, i% - 1)
        LET list$ = MID$(list$, i% + 1)
    END IF
END FUNCTION

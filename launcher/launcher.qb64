'copyright (C) Kroc Camen 2018, BSD 2-clause

'third-party library declarations:
'-----------------------------------------------------------------------------
'$INCLUDE:'lib\INI-Manager\ini.bi'

'our library declarations:
'-----------------------------------------------------------------------------
'$INCLUDE:'lib\ascii.bi'
'$INCLUDE:'lib\consts.bi'
'$INCLUDE:'lib\strings.bi'
'$INCLUDE:'lib\reg.bi'

'PortaDOOM Launcher declarations:
'-----------------------------------------------------------------------------
'$INCLUDE:'inc\tags.bi'
'$INCLUDE:'inc\games.bi'
'$INCLUDE:'inc\engines.bi'
'$INCLUDE:'inc\iwads.bi'

'=============================================================================

'generic 'index' counter
DIM i AS _UNSIGNED LONG

'temp variable for reading `INKEY$`
DIM key$

'the folder where config files are kept
CONST DIR_CONFIG$ = "config\"

'folder for the 'source ports' -- game engines
CONST DIR_PORTS$ = "ports\"

'folder for the user's save files
CONST DIR_SAVES$ = "saves\"

'folder for external executables;
'such as patching utilities
CONST DIR_TOOLS$ = "tools\"

'folder for WADs -- the game files to play
CONST DIR_WADS$ = "wads\"

'folder for demo-files (i.e. recorded runs)
CONST DIR_DEMOS$ = "demos\"

'folder for gameplay mods
CONST DIR_MODS$ = "mods\"

'read system info:
'-----------------------------------------------------------------------------
'get CPU type for the system (32 / 64-bit)
DIM SHARED CPU_BIT AS _UNSIGNED LONG
'default to 32-bit as this will always work
LET CPU_BIT = 32
'check the environment variables for CPU type:
'this one would only be true if we are a 64-bit executable also
IF ENVIRON$("PROCESSOR_ARCHITECTURE") = "AMD64" THEN LET CPU_BIT = 64
'detect 64-bit system from a 32-bit executable (WOW64)
IF ENVIRON$("PROCESSOR_ARCHITEW6432") = "AMD64" THEN LET CPU_BIT = 64

'directory of this executable (regardless of where it was called from)
DIM SHARED DIR_EXE$
LET DIR_EXE$ = Paths_AddSlash$(_CWD$)

'now, is this executable running from the source code folder or from within
'PortaDOOM's folder (as it would be in releases)? since these are different
'directories, and the launcher operates on the assumption that it's in the
'"PortaDOOM\files" folder, we need to re-route things when running from
'development builds. check to see if our parent folder is not "files":

IF Paths_GetFolderName$(DIR_EXE$) = "launcher" THEN
    'change directory to the expected location
    CHDIR "..\PortaDOOM\files"
    LET DIR_EXE$ = Paths_AddSlash$(_CWD$)
END IF

'the 'current directory' (where this executable was called *from*),
'this will be one of the folders we check when searching for files
DIM SHARED DIR_CUR$
LET DIR_CUR$ = Paths_AddSlash$(_STARTDIR$)

'=============================================================================
'MAIN:
'=============================================================================
'$INCLUDE:'inc\ui_init.bi'

COLOR BLACK, UI_FORECOLOR
''PRINT " " + PAD$("PortaDOOM Launcher", UI_SCREEN_WIDTH - 2) + " "
LOCATE UI_SCREEN_HEIGHT, 1: PRINT SPACE$(UI_SCREEN_WIDTH);
LOCATE 1, 1: PRINT SPACE$(UI_SCREEN_WIDTH)

COLOR UI_FORECOLOR, UI_BACKCOLOR
VIEW PRINT 2 TO UI_SCREEN_HEIGHT - 2
CLS 2: PRINT ""

''DIM SHARED glVersion$
''
''DIM start!
''LET start! = TIMER
''DO
''LOOP UNTIL LEN(glVersion$) > 0 OR TIMER - start! > .3
''
''PRINT glVersion$
''PRINT ""

'process command line / enumerate games:
'-----------------------------------------------------------------------------
'$INCLUDE:'inc\cmd.bm'

'select game:
'-----------------------------------------------------------------------------
'if only one game is defined, we don't need to offer a choice
IF Games_Count =1 THEN
    CALL Games_Select(1)
    GOTO enumerate
END IF

'present game selection UI:
'walk through the list of games

CLS 2
PRINT ""
COLOR UI_FORECOLOR
PRINT " Select game choice by pressing indicated number key:"
PRINT ""

FOR i = 1 TO Games_Count
    COLOR AQUA: PRINT " [" + STRINT$(i) + "] ";: COLOR YELLOW
    PRINT TRUNCATE$(STRGET$(Games(i).title), UI_SCREEN_WIDTH - 5 - 2)
    PRINT " " + STRING$(UI_SCREEN_WIDTH - 2, &HC4)
    COLOR UI_FORECOLOR
    IF Games(i).desc <> 0 THEN
        DIM A
        LET A = PRINTWRAP%(2, CSRLIN, UI_SCREEN_WIDTH - 2, STRGET$(Games(i).desc))
    END IF
    PRINT ""
NEXT i

choose:
DO
    LET key$ = INKEY$
LOOP WHILE ISINT(key$) = FALSE

IF VAL(key$) < 1 OR VAL(key$) > Games_Count THEN
    GOTO choose
END IF

Games_Select(VAL(key$))


enumerate:
'-----------------------------------------------------------------------------
'search through the "ports" folder for game engines and read in their details.
'this also builds a set of look-up tables for cross-referencing tags with
'games and engines so that we can filter out incompatible engines
CALL Engines_Enumerate
CALL Engines_Filter

launch:
'=============================================================================
'$INCLUDE:'inc\launch.bm'

'=============================================================================

''SUB _GL
''    IF LEN(glVersion$) > 0 THEN EXIT SUB
''    LET glVersion$ = _glGetString(_GL_VERSION)
''END SUB

SUB PRINTWRAP_X(x~%%, w~%%, text$)
    DIM NEWPOS%
    LET NEWPOS% = PRINTWRAP%(x~%%, CSRLIN, w~%%,text$)
END SUB

'print a message to screen, handling word-wrapping and boundaries.
'returns the final line-count actually used
'=============================================================================
FUNCTION PRINTWRAP%(x~%%, y~%%, w~%%, text$)
    LOCATE y~%%, x~%%
    
    DIM last_spc%, char~%%
    DIM line_len%
    DIM line_count%
    LET line_count% = 1
    
    LET text$ = text$ + " "
    
    DIM i AS _UNSIGNED LONG
    LET i = 1: LET last_spc% = 1
    DO
        'get a single character from the source string
        LET char~%% = ASC(text$, i)
        'is this a space?
        IF char~%% = ASC_SPC THEN
            'does the current word over-hang?
            IF line_len% >= w~%% THEN
                'move to the next line
                LET y~%% = y~%% + 1
                LOCATE y~%%, x~%%
                
                LET line_count% = line_count% + 1
                
                'print the current word & space and continue
                PRINT MID$(text$, last_spc% + 1, i - last_spc% - 1);
                'the length of the new line is now this word+space
                LET line_len% = i - last_spc%
                'set this space as the next available break-point
                LET last_spc% = i
            ELSE
                PRINT MID$(text$, last_spc%, i - last_spc%);
                
                LET last_spc% = i
                LET line_len% = line_len% + 1
            END IF
        ELSE
            LET line_len% = line_len% + 1
        END IF
        
        LET i = i + 1
    LOOP WHILE i <= LEN(text$)
    PRINT ""
    
    LET PRINTWRAP% = line_count%
END FUNCTION

'=============================================================================
'PortaDOOM Launcher declarations:
'-----------------------------------------------------------------------------
'$INCLUDE:'inc\error.bm'
'$INCLUDE:'inc\tags.bm'
'$INCLUDE:'inc\games.bm'
'$INCLUDE:'inc\engines.bm'
'$INCLUDE:'inc\iwads.bm'
'$INCLUDE:'inc\wads.bm'

'third-party library functions:
'-----------------------------------------------------------------------------
'$INCLUDE:'lib\INI-Manager\ini.bm'

'our library functions:
'-----------------------------------------------------------------------------
'$INCLUDE:'lib\strings.bm'
'$INCLUDE:'lib\paths.bm'
'$INCLUDE:'lib\reg.bm'

'copyright (C) Kroc Camen 2018, BSD 2-clause
'tags.bm : functions for managing engine/game feature tags

'   Tags_Add(tagList$)
'   Tags_Find%(tag$)        -> INTEGER
'   Tags_Split$(tagList$)   -> STRING

'process a comma-separated list of tags, adding them to the known tag list
'=============================================================================
SUB Tags_Add (tagList$)
    'the tag list will be broken into individual tags,
    'these will hold the individual tag index and name
    DIM tag%, tag$
    
    'prime the iterator
    LET tag$ = Tags_Split$(tagList$)
    
    DO WHILE tag$ <> ""
        'check if the tag already exists:
        IF Tags_Find%(tag$) <> 0 THEN GOTO NextTag
        
        'increase the number of known tags
        LET tag% = UBOUND(Tags) + 1
        REDIM _PRESERVE Tags(1 TO tag%) AS STRING
        LET Tags(tag%) = tag$
        
        NextTag:
        LET tag$ = Tags_Split$("")
    LOOP
END SUB

'search through the tag list and return an index number
'=============================================================================
FUNCTION Tags_Find% (tag$)
    'TODO: this could be done by index only
    FOR Tags_Find% = 1 TO UBOUND(Tags)
        IF Tags(Tags_Find%) = tag$ THEN EXIT FUNCTION
    NEXT
    
    'not found
    LET Tags_Find% = 0
END FUNCTION

'this function, once given a list of comma-separated tags will return one tag
'each time the function is called until none remain
'=============================================================================
FUNCTION Tags_Split$ (tagList$)
    'if a list is provided, keep it for each additional call to the function
    STATIC list$
    IF tagList$ <> "" THEN LET list$ = tagList$
    
    'if no tags remain, then return nothing
    IF list$ = "" THEN EXIT FUNCTION
    
    'find the next comma
    DIM i%
    LET i% = INSTR(1, list$, ",")
    
    'if there are no commas in the tag-list, it's just one tag
    IF i% = 0 THEN
        'return the final tag
        LET Tags_Split$ = list$
        'clear the remaining list
        LET list$ = ""
    ELSE
        'strip off the current tag
        LET Tags_Split$ = LEFT$(list$, i% - 1)
        LET list$ = MID$(list$, i% + 1)
    END IF
END FUNCTION

'copyright (C) Kroc Camen 2018, BSD 2-clause

'   Engines_Enumerate()
'   Engines_KinValue%(kin$*1)   -> INTEGER
'   Engines_Filter()
'   Engines_Select(engine%)

'walk the ports folder and identify all engines present
'=============================================================================
SUB Engines_Enumerate()
    'loop counter
    DIM i AS _UNSIGNED LONG
    
    LET Engines_Count = 0
    
    'create a directory list in a file, containing only sub-folder names
    CHDIR DIR_EXE$ + "ports"
    SHELL _HIDE "DIR /AD /B > dir.tmp"
    
    DIM dir_handle%
    LET dir_handle% = FREEFILE
    OPEN "dir.tmp" FOR INPUT AS #dir_handle%
    
    DO WHILE EOF(dir_handle%) = FALSE
        'read a folder-name
        DIM folder$
        LINE INPUT #dir_handle%, folder$
        
        'check for an INI file in the engine's folder
        DIM ini_path$
        LET ini_path$ = _CWD$ + "\" + folder$ + "\porta-doom.ini"
        IF NOT _FILEEXISTS(ini_path$) THEN GOTO NextFolder
        
        'read the global values
        DIM ini_name$
        LET ini_name$ = ReadSetting$(ini_path$, "global", "name")
        DIM ini_rank$
        LET ini_rank$ = ReadSetting$(ini_path$, "global", "rank")
        
        DIM ini_index%
        LET ini_index% = 1
        DO
            DIM ini_engine$
            LET ini_engine$ = ReadSetting$("", "engines", STRINT$(ini_index%))
            IF ini_engine$ = "" THEN EXIT DO
            
            'increase the list of known engines
            LET Engines_Count = Engines_Count + 1
            REDIM _PRESERVE Engines(1 TO Engines_Count) AS Engine
            LET Engines(Engines_Count).id = ini_engine$
            
            'read engine data from the INI file
            DIM ini_section$
            LET ini_section$ = "engine." + ini_engine$
            
            DIM ini_title$
            LET ini_title$ = ReadSetting$("", ini_section$, "title")
            DIM ini_exe$
            LET ini_exe$ = ReadSetting$("", ini_section$, "exe")
            DIM ini_ver$
            LET ini_ver$ = ReadSetting$("", ini_section$, "ver")
            DIM ini_bit$
            LET ini_bit$ = ReadSetting$("", ini_section$, "bit")
            DIM ini_vid$
            LET ini_vid$ = ReadSetting$("", ini_section$, "vid")
            DIM ini_kin$
            LET ini_kin$ = ReadSetting$("", ini_section$, "kin")
            DIM ini_cfg$
            LET ini_cfg$ = ReadSetting$("", ini_section$, "cfg")
            DIM ini_save$
            LET ini_save$ = ReadSetting$("", ini_section$, "save")
            DIM ini_tags$
            LET ini_tags$ = ReadSetting$("", ini_section$, "tags")
            DIM ini_iwads$
            LET ini_iwads$ = ReadSetting$("", ini_section$, "iwads")
            DIM ini_pwads$
            LET ini_pwads$ = ReadSetting$("", ini_section$, "pwads")
            DIM ini_auto$
            LET ini_auto$ = ReadSetting$("", ini_section$, "auto")
            
            'executable architecture must be either 32 or 64-bit
            IF ini_bit$ <> "32" AND ini_bit$ <> "64" THEN
                'TODO: output warning
                'default to 32-bit
                LET ini_bit$ = "32"
            END IF
            
            'process the tag list
            CALL Tags_Add(ini_tags$)
            
            LET Engines(Engines_Count).folder = folder$
            LET Engines(Engines_Count).name = ini_name$
            LET Engines(Engines_Count).rank = VAL(ini_rank$)
            LET Engines(Engines_Count).title = ini_title$
            LET Engines(Engines_Count).exe = ini_exe$
            LET Engines(Engines_Count).ver = VAL(ini_ver$)
            LET Engines(Engines_Count).bit = VAL(ini_bit$)
            LET Engines(Engines_Count).vid = VAL(ini_vid$)
            SELECT CASE ini_kin$
                CASE "X": LET Engines(Engines_Count).kin = KIN_X
                CASE "V": LET Engines(Engines_Count).kin = KIN_V
                CASE "B": LET Engines(Engines_Count).kin = KIN_B
                CASE "Z": LET Engines(Engines_Count).kin = KIN_Z
                CASE "G": LET Engines(Engines_Count).kin = KIN_G
                CASE ELSE
                    LET Engines(Engines_Count).kin = 0
            END SELECT
            LET Engines(Engines_Count).cfg = ini_cfg$
            LET Engines(Engines_Count).save = ini_save$
            LET Engines(Engines_Count).tags = ini_tags$
            LET Engines(Engines_Count).iwads = ini_iwads$
            LET Engines(Engines_Count).pwads = ini_pwads$
            LET Engines(Engines_Count).auto = ini_auto$
            
            LET ini_index% = ini_index% + 1
        LOOP
    NextFolder:
    LOOP
    
    CLOSE #dir_handle%
    KILL "dir.tmp"
	CHDIR DIR_EXE$
    
    'prioritise engine list:
    '-------------------------------------------------------------------------
    '... given ten different versions of GZDoom, each with hardware/software
    'renderers, which is the 'best' that we should default to?
    
    'the criteria for the 'best' engine is:
    '- prefer engines with a higher rank
    '- prefer engines in a higher genealogy, i.e. "G" > "Z" > "B" > "V"
    '- prefer newer versions over older versions
    '- prefer hardware renderers (if present) over software
    '- prefer 64-bit (if present and machine supports it) over 32-bit
    
    'to whit, re-order the list of engines according to this criteria:
    
    DIM sorted%
    LET sorted% = FALSE
    DIM sortCount%
    
    DO WHILE sorted% = FALSE
        LET sorted% = TRUE
        
        FOR i = 2 TO Engines_Count
            'compare the engine with the one above
            '(A = above, B = below)
            DIM A%, B%
            LET A% = i - 1
            LET B% = i
            
            'compare the engine's rank:
            DIM rA%, rB%
            LET rA% = Engines(A%).rank
            LET rB% = Engines(B%).rank
            
            'if the engine below is of a higher rank, move it up
            IF rB% > rA% THEN
                SWAP Engines(A%), Engines(B%)
                LET sortCount% = sortCount% + 1
                LET sorted% = FALSE
                
            ELSEIF rB% = rA% THEN
                'compare the two engine's genealogy:
                DIM kA%, kB%
                LET kA% = Engines(A%).kin
                LET kB% = Engines(B%).kin
                
                'if the engine below is of a higher genealogy, move it up
                IF kB% > kA% THEN
                    SWAP Engines(A%), Engines(B%)
                    LET sortCount% = sortCount% + 1
                    LET sorted% = FALSE
                
                ELSEIF kB% = kA% THEN
                    'compare the names (alphabetically sort engines)
                    DIM nA$, nB$
                    LET nA$ = Engines(A%).name
                    LET nB$ = Engines(B%).name
                    
                    IF nB$ < nA$ THEN
                        SWAP Engines(A%), Engines(B%)
                        LET sortCount% = sortCount% + 1
                        LET sorted% = FALSE
                    
                    ELSEIF nB$ = nA$ THEN
                        'compare the two engine's version numbers
                        DIM vA%, vB%
                        LET vA% = Engines(A%).ver
                        LET vB% = Engines(B%).ver
                        
                        IF vB% > vA% THEN
                            SWAP Engines(A%), Engines(B%)
                            LET sortCount% = sortCount% + 1
                            LET sorted% = FALSE
                            
                        ELSEIF vB% = vA% THEN
                            'compare renderer colour-depth
                            DIM cA%, cB%
                            LET cA% = Engines(A%).vid
                            LET cB% = Engines(B%).vid
                            
                            IF cB% > cA% THEN
                                SWAP Engines(A%), Engines(B%)
                                LET sortCount% = sortCount% + 1
                                LET sorted% = FALSE
                            
                            ELSEIF cB% = cA% THEN
                                'compare executable CPU-type (32/64-bit)
                                DIM xA%, xB%
                                LET xA% = Engines(A%).bit
                                LET xB% = Engines(B%).bit
                                
                                IF xB% > xA% THEN
                                    SWAP Engines(A%), Engines(B%)
                                    LET sortCount% = sortCount% + 1
                                    LET sorted% = FALSE
                                END IF
                            END IF
                            
                        END IF
                    END IF
                END IF
            END IF
        NEXT i
        
    LOOP
    
    '-------------------------------------------------------------------------
    
    'create a look-up table of which engines provide which tags
    REDIM EngineTags(1 TO Engines_Count, 0 TO UBOUND(Tags)) AS LONG
    FOR i = 1 TO UBOUND(Engines)
        LET tag$ = Tags_Split$(Engines(i).tags)
        DO WHILE tag$ <> ""
            'find the tag index
            LET tag% = Tags_Find%(tag$)
            'set up the game <> tag mapping
            LET EngineTags(i, tag%) = 1
            'get the next tag in the list
            LET tag$ = Tags_Split$("")
        LOOP
    NEXT i
    
    'also, do this for game definitions -- this has to be done after
    'enumerating the engines, as all unique tags will be known by then
    REDIM GameTags(1 TO Games_Count, 1 TO UBOUND(Tags)) AS LONG
    FOR i = 1 TO UBOUND(Games)
        LET tag$ = Tags_Split$(Games(i).tags)
        DO WHILE tag$ <> ""
            'find the tag index
            LET tag% = Tags_Find%(tag$)
            'set up the game <> tag mapping
            LET GameTags(i, tag%) = 1
            'get the next tag in the list
            LET tag$ = Tags_Split$("")
        LOOP
    NEXT i
    
END SUB

'filter the engines to suit the chosen game
'=============================================================================
SUB Engines_Filter()
    'loop counter
    DIM i AS _UNSIGNED LONG
	
    'test each engine against the game's requirements:
    FOR i = 1 TO UBOUND(Engines)    
        'are we looking for a specific engine-id?
        'e.g. the `/USE` command-line parameter
        IF CMD_USE$ <> "" THEN
            IF RTRIM$(Engines(i).name) <> CMD_USE$ THEN
				IF RTRIM$(Engines(i).id) <> CMD_USE$ THEN
					_CONTINUE
				END IF
            END IF
        END IF
        
        'is the binary compatible with this machine? (32 / 64-bit)
        'if we are a 32-bit machine, we can't run 64-bit software
        IF CPU_BIT = 32 AND Engines(i).bit = 64 THEN
            _CONTINUE
        END IF
        
        '-------------------------------------------------------------------------
        
        'TODO: validate engine IWAD / PWAD support
        
        
        '-------------------------------------------------------------------------
        
        'does this engine provide all the features required by the game?
        '(walk along the tags on the engine)
        FOR tag% = 1 TO UBOUND(Tags)
            'check the game's requirement for this tag
            SELECT CASE GameTags(Games_Current, tag%)
                'the *game* requires this from the engine
                CASE 1:
                    'if the *engine* does not provide this feature,
                    'move on to the next engine
                    IF EngineTags(i, tag%) = 0 THEN
                        '`_CONTINUE` does not support breaking nested loops
                        LET i = i + 1: GOTO NextEngine
                    END IF
            END SELECT
        NEXT tag%
        
        '-------------------------------------------------------------------------
        
        'all tags have been checked, meaning that this engine is a candidate
        LET EngineTags(i, 0) = 1
        'add this tag to the valid short-list
        LET Engines_ListCount = Engines_ListCount + 1
        REDIM _PRESERVE Engines_List(1 TO Engines_ListCount) AS INTEGER
        LET Engines_List(Engines_ListCount) = i
		
    NextEngine:
    NEXT i

    'print a list of the candidate engines:
    _ECHO ""
    _ECHO "Candidate Engines:"
    _ECHO "------------------"
    FOR i = 1 TO Engines_ListCount
        _ECHO "- " + Engines(Engines_List(i)).id + ": " _
            + Engines(Engines_List(i)).title
    NEXT i
    _ECHO ""
    
    'select the 'best' engine by default
    CALL Engines_Select(Engines_List(1))
END SUB

'selects an engine for use, populating the 'selected engine' variables
'============================================================================
SUB Engines_Select(engine%)
    LET Engines_Current = engine%
    LET Engines_Selected = Engines(Engines_Current)
END SUB

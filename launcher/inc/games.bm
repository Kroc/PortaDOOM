'copyright (C) Kroc Camen 2018, BSD 2-clause

'   Games_EnumerateINI(INIFile$)
'   Games_Add(...)
'   Games_Select(game%)

'given an INI file, read game definitions from it
'=============================================================================
SUB Games_EnumerateINI (INIFile$)
    'locate the INI file:
    '-------------------------------------------------------------------------
    DIM path$
    LET path$ = INIFile$
    
    'is the given path absolute?
    IF Paths_IsAbsolute(path$) THEN
        'does the file exist?
        IF _FILEEXISTS(path$) THEN GOTO readINI
    ELSE
        'search for the file:
        
        'check the 'current' directory
        '-- i.e. the directory the launcher was called *from*
        LET path$ = DIR_CUR$ + INIFile$
        IF _FILEEXISTS(path$) THEN GOTO readINI
        
        'check the base WADs directory
        LET path$ = DIR_WADS$ + INIFile$
        IF _FILEEXISTS(path$) THEN GOTO readINI
    END IF
    
    'FIXME: file not found!
    ERROR 100
    
readINI:
    '-------------------------------------------------------------------------
    DO
        'read from the list of game-configurations defined
        '(a single INI can define more than one game variation)
        DIM game_id$
        LET game_id$ = ReadSetting$(path$, "games", STRINT$(Games_Count + 1))
        'if there's no further entries, stop looking
        IF game_id$ = "" THEN EXIT DO
        
        'read in the game details:
        'TODO: validation
        'TODO: early rejection of engines incompatible with all game defs,
        '      e.g. 64-bit engine on a 32-bit CPU
        DIM ini_section$
        LET ini_section$ = "settings." + game_id$
        DIM ini_title$
        LET ini_title$ = ReadSetting$("", ini_section$, "title")
		DIM ini_cmplvl$
        LET ini_cmplvl$ = ReadSetting$("", ini_section$, "cmplvl")
        DIM ini_iwad$
        LET ini_iwad$ = ReadSetting$("", ini_section$, "iwad")
        DIM ini_pwad$
        LET ini_pwad$ = ReadSetting$("", ini_section$, "pwad")
		DIM ini_files$
		LET ini_files$ = ReadSetting$("", ini_section$, "files")
        DIM ini_deh$
        LET ini_deh$ = ReadSetting$("", ini_section$, "deh")
        DIM ini_bex$
        LET ini_bex$ = ReadSetting$("", ini_section$, "bex")
        DIM ini_tags$
        LET ini_tags$ = ReadSetting$("", ini_section$, "tags")
        DIM ini_vid$
        LET ini_vid$ = ReadSetting$("", ini_section$, "vid")
        
        CALL Games_Add( _
            game_id$, ini_title$, ini_cmplvl$, _
            ini_iwad$, ini_pwad$, ini_files$, _
			ini_deh$, ini_bex$, _
            ini_tags$, ini_vid$ _
        )
    LOOP
END SUB

'=============================================================================
SUB Games_Add ( _
    id$, title$, cmplvl$, iwad$, pwad$, files$, deh$, bex$, tags$, vid$ _
)
    'increase the number of games entries
    LET Games_Count = Games_Count + 1
    REDIM _PRESERVE Games(1 TO Games_Count) AS Game
    LET Games(Games_Count).id = STRADD&(id$)
    
    ''_ECHO "* GAME #" + STRINT$(Games_Count) + ": " + id$
    ''IF title$ <> "" THEN _ECHO "- " + id$ + ".title" + CHR$(9) + title$
	''IF cmplvl$ <> "" THEN _ECHO "- " + id$ + ".cmplvl" + CHR$(9) + cmplvl$
    ''IF iwad$ <> "" THEN _ECHO "- " + id$ + ".iwad" + CHR$(9) + iwad$
    ''IF pwad$ <> "" THEN _ECHO "- " + id$ + ".pwad" + CHR$(9) + pwad$
	''IF files$ <> "" THEN _ECHO "- " + id$ + ".files" + CHR$(9) + files$
    ''IF deh$ <> "" THEN _ECHO "- " + id$ + ".deh" + CHR$(9) + deh$
    ''IF bex$ <> "" THEN _ECHO "- " + id$ + ".bex" + CHR$(9) + bex$
    ''IF tags$ <> "" THEN _ECHO "- " + id$ + ".tags" + CHR$(9) + tags$
    ''IF vid$ <> "" THEN _ECHO "- " + id$ + ".vid" + CHR$(9) + vid$
    
    'process the tag list
    CALL Tags_Add(tags$)
    
    'no problems? write the data into the engine list
    LET Games(Games_Count).title = STRADD&(title$)
	IF cmplvl$ = "" THEN
        LET Games(Games_Count).cmplvl = -1
    ELSE
        LET Games(Games_Count).cmplvl = VAL(cmplvl$)
    END IF
    LET Games(Games_Count).iwad = STRADD&(iwad$)
    LET Games(Games_Count).pwad = STRADD&(pwad$)
	LET Games(Games_Count).files = STRADD&(files$)
    LET Games(Games_Count).deh = STRADD&(deh$)
    LET Games(Games_Count).bex = STRADD&(bex$)
    LET Games(Games_Count).tags = STRADD&(tags$)
    LET Games(Games_Count).vid = VAL(vid$)
END SUB

'=============================================================================
SUB Games_Select(game%)
    LET Games_Current = game%
    LET Games_Selected = Games(Games_Current)
END SUB

'copyright (C) Kroc Camen 2018, BSD 2-clause

'=============================================================================
'launch the selected game, with the selected engine, with the selected
'parameters, any mods (if selected) and so forth. we need to build the
'right command-line to do all of this
'
'   [01]: save game path
'   [02]: engine executable
'   [03]: config file
'   [04]: compatibility level
'   [05]: IWAD
'   [06]: auto-load files
'   [07]: PWAD
'   [08]: file-list
'   [09]: DEH & BEX
'   [10]: demo playback
'   [11]: warp & skill
'   [12]: execute scripts
'   [13]: resolution & full-screen
'   [14]: launch!

CLS
PRINT ""

'NOTE: many doom engines save their config files in the 'current directory',
'which is typically expected to be that of the executable. however, we want
'to separate user-data (such as save-games) from the engines. whilst we can
'change the save directory, config files will still be dumped in the 'current
'directory'.

'we therefore need to change the current directory and rewrite
'all the WAD / file paths to be relative from there!

'this is the relative path from the WAD's
'save folder back to this executable
DIM SHARED FIX_PATH$
LET FIX_PATH$ = "..\..\..\"

'we will remember the directory of the last file
'(used for finding side-by-side WADs)
DIM SHARED DIR_PREV$
'other directories that will be remembered as we go
DIM SHARED DIR_IWAD$
DIM SHARED DIR_PWAD$

'some engines require different methods of passing extra files, e.g. PWAD
'and mods. as we go, we'll build up a list of files to include (in the right
'order) and worry about the specific invocation at the end
LET CMD_FILES$ = ""

'-----------------------------------------------------------------------------
'[01] save game path:
'-----------------------------------------------------------------------------
'the "saves" folder should exist (it's part of PortaDOOM),
'but just in case, check
CALL Launch_MkDir(DIR_EXE$ + DIR_SAVES$)

'add the save-folder name for the selected engine; there can be
'multiple folder names and IDs for engines, such as 32/64-bit and
'differing versions, so a common save name is provided in the INI
DIM DIR_SAVE_ENGINE$
LET DIR_SAVE_ENGINE$ = Paths_AddSlash$( _
    DIR_SAVES$ + STRGET$(Engines_Selected.save) _
)

'does this folder exist?
CALL Launch_MkDir(DIR_EXE$ + DIR_SAVE_ENGINE$)

'the saves folder will contain a sub-folder for each engine (above),
'and then another sub-folder for the IWAD or PWAD
DIM DIR_SAVE_GAME$

'if the game is using a PWAD,
'use that name for the save folder
IF Games_Selected.pwad <> 0 THEN
    LET DIR_SAVE_GAME$ = DIR_SAVE_ENGINE$ _
        + Paths_GetFileBaseName$(STRGET$(Games_Selected.pwad))
ELSE
    'otherwise just use the IWAD name
    
    LET DIR_SAVE_GAME$ = DIR_SAVE_ENGINE$ _
        + Paths_GetFileBaseName$(STRGET$(IWADs_Selected.path))
END IF

'does this folder exist?
CALL Launch_MkDir(DIR_EXE$ + DIR_SAVE_GAME$)

'-----------------------------------------------------------------------------
'[02] engine executable:
'-----------------------------------------------------------------------------
DIM DIR_ENGINE$
LET DIR_ENGINE$ = DIR_PORTS$ + Paths_AddSlash$(STRGET$(Engines_Selected.dir))

'add the engine executable to the command-line
LET CMD$ = CHR$(34) _
         + Launch_FixPath$(DIR_ENGINE$) + STRGET$(Engines_Selected.exe) _
         + CHR$(34)

COLOR YELLOW: PRINT "         port : ";: COLOR UI_FORECOLOR
PRINT RTRUNCATE$(STRGET$(Engines_Selected.title), UI_SCREEN_WIDTH - 17)
COLOR YELLOW: PRINT "       engine : ";: COLOR UI_FORECOLOR
PRINT RTRUNCATE$(DIR_ENGINE$ + STRGET$(Engines_Selected.exe), UI_SCREEN_WIDTH - 17)
PRINT ""

'-----------------------------------------------------------------------------
'[03] config file:
'-----------------------------------------------------------------------------
DIM CMD_CONFIG$

'which file-extension does the engine use? ZDoom-based engines
'use ".ini", everything else ".cfg" (including DOOM64EX)
DIM cfg$
IF Engines_Selected.kin >= KIN_Z THEN
    LET cfg$ = ".ini"
ELSE
    LET cfg$ = ".cfg"
END IF

'define the default config file path, as we will need to copy this if the
'engine's config file doesn't exist yet. note that this leaves off the
'file extension, for now
DIM cfg_default$
LET cfg_default$ = DIR_CONFIG$ + "default." + STRGET$(Engines_Selected.cfg)

'was the command-line parameter given to use the default config-file?
IF CMD_DEFAULT` THEN
    'set the path to the default config file (sans-extension)
    LET CMD_CONFIG$ = cfg_default$
ELSE
    'build the path to the user's config file (for selected engine)
    'note that this leaves off the file extension, for now
    DIM cfg_engine$
    LET cfg_engine$ = DIR_SAVE_ENGINE$ _
                    + "config." + STRGET$(Engines_Selected.cfg)

    'if the user's config file doesn't exist, make a copy of the default
    IF NOT _FILEEXISTS(cfg_engine$ + cfg$) THEN
        'copy across the default config file
        'FIXME: capture error here
        SHELL _HIDE "COPY /Y " _
            + CHR$(34) + cfg_default$ + cfg$ + CHR$(34) + " /A " _
            + CHR$(34) + cfg_engine$ + cfg$ + CHR$(34) + " /A"
        
        'vanilla engines store non-original settings in an extra config file
        IF Engines_Selected.kin = KIN_V THEN
            'FIXME: capture error here
            SHELL _HIDE "COPY /Y " _
                + CHR$(34) + cfg_default$ + ".extra" + cfg$ + CHR$(34) + " /A " _
                + CHR$(34) + cfg_engine$ + ".extra" + cfg$ + CHR$(34) + " /A"
        END IF
    END IF
    
    LET CMD_CONFIG$ = cfg_engine$
END IF

'add the command-line parameter to select the config file
LET CMD$ = CMD$ + " -config " _
         + CHR$(34) + Launch_FixPath$(CMD_CONFIG$) + cfg$ + CHR$(34)
COLOR YELLOW: PRINT "      -config : ";: COLOR UI_FORECOLOR
PRINT RTRUNCATE$(CMD_CONFIG$ + cfg$, UI_SCREEN_WIDTH - 17)

'vanilla engines (Chocolate-Doom, Crispy-Doom) store non-original settings
'in an extra config file (this is why we omit the file extension until now)
IF Engines_Selected.kin = KIN_V THEN
    LET CMD$ = CMD$ + " -extraconfig " _
             + CHR$(34) + Launch_FixPath$(CMD_CONFIG$ + ".extra" + cfg$) + CHR$(34)
    
    COLOR YELLOW: PRINT " -extraconfig : ";: COLOR UI_FORECOLOR
    PRINT RTRUNCATE$(CMD_CONFIG$ + ".extra" + cfg$, UI_SCREEN_WIDTH - 17)
END IF

'-----------------------------------------------------------------------------
'[04] compatibility level:
'-----------------------------------------------------------------------------
IF Games_Selected.cmplvl >= 0 THEN
    'TODO: should this only be applied to Boom-based engines?
    LET CMD$ = CMD$ + " -complevel " + STRINT$(Games_Selected.cmplvl)
    
    COLOR YELLOW: PRINT "   -complevel : ";: COLOR UI_FORECOLOR
    PRINT STRINT$(Games_Selected.cmplvl)
END IF

'-----------------------------------------------------------------------------
'[05] IWAD:
'-----------------------------------------------------------------------------
'try and locate the IWAD path
'FIXME: handle FreeDOOM replacement / BFG-edition patching
LET CMD_IWAD$ = IWADs_GetPath$(STRGET$(IWADs_Selected.path))
LET DIR_IWAD$ = Paths_GetPath$(CMD_IWAD$)

LET CMD_IWAD$ = Launch_ClipPath$(CMD_IWAD$)
LET CMD$ = CMD$ + " -iwad " _
         + CHR$(34) + Launch_FixPath$(CMD_IWAD$) + CHR$(34)
         
COLOR YELLOW: PRINT "        -iwad : ";: COLOR UI_FORECOLOR
PRINT RTRUNCATE$(CMD_IWAD$, UI_SCREEN_WIDTH - 17)

'-----------------------------------------------------------------------------
'[06] auto-load files:
'-----------------------------------------------------------------------------
'any files that should be automatically included with the engine?
'(e.g. "brightmaps.pk3", "lights.pk3")
DIM auto_path$
LET auto_path$ = STRGET$(Engines_Selected.auto)
IF auto_path$ <> "" THEN
    'read the first file from the list
    LET auto_path$ = WADs_Split$(auto_path$)
    DO
        'FIXME: check file exists; here or at the end?
        'TODO: other places for auto-load files?
        
        'add to the list of files for the command-line params
        LET CMD_FILES$ = CMD_FILES$ + DIR_ENGINE$ + auto_path$ + ";"
        
        COLOR YELLOW: PRINT " (auto) -file : ";: COLOR UI_FORECOLOR
        PRINT RTRUNCATE$(Launch_ClipPath$(DIR_ENGINE$ + auto_path$), UI_SCREEN_WIDTH - 17)
        
        LET auto_path$ = WADs_Split$("")
    LOOP WHILE auto_path$ <> ""
END IF

'-----------------------------------------------------------------------------
'[07] PWAD:
'-----------------------------------------------------------------------------
'doom engines typically don't have a specific concept of a "PWAD", it's just
'an additional file to load like any other and it's not necessarily the first
'listed -- that will likely be the auto-load files

LET CMD_PWAD$ = STRGET$(Games_Selected.pwad)
IF CMD_PWAD$ <> "" THEN
    LET CMD_PWAD$ = WADs_GetPWADPath$(CMD_PWAD$)
    'FIXME: file missing?
    LET DIR_PWAD$ = Paths_GetPath$(CMD_PWAD$)
    
    CMD_PWAD$ = Launch_ClipPath$(CMD_PWAD$)
    LET CMD_FILES$ = CMD_FILES$ + CMD_PWAD$ + ";"
    
    COLOR YELLOW: PRINT " (pwad) -file : ";: COLOR UI_FORECOLOR
    PRINT RTRUNCATE$(CMD_PWAD$, UI_SCREEN_WIDTH - 17)
END IF

'-----------------------------------------------------------------------------
'[08] file-list:
'-----------------------------------------------------------------------------
DIM file$
LET file$ = STRGET$(Games_Selected.files)
IF file$ <> "" THEN
    'read the first file from the list
    LET file$ = WADs_Split$(file$)
    DO
        LET file$ = Launch_ClipPath$(file$)
        LET file$ = Launch_ClipPath$(WADs_Find$(file$))
        'add to the list of files for the command-line params
        LET CMD_FILES$ = CMD_FILES$ + file$ + ";"
        'announce
        COLOR YELLOW: PRINT "        -file : ";: COLOR UI_FORECOLOR
        PRINT RTRUNCATE$(file$, UI_SCREEN_WIDTH - 17)
        'get the next file in the list (if any)
        LET file$ = WADs_Split$("")
    LOOP WHILE file$ <> ""
END IF

'add all files to the command-line
IF CMD_FILES$ <> "" THEN
    'vanilla engines use `-merge`
    IF Engines_Selected.kin = KIN_V THEN
        CMD$ = CMD$ + " -merge"
    ELSE
        CMD$ = CMD$ + " -file"
    END IF
    
    'read the first file from the list
    LET file$ = WADs_Split$(CMD_FILES$)
    DO
        'add the file to the command-line
        LET CMD$ = CMD$ + " " + CHR$(34) + Launch_FixPath$(file$) + CHR$(34)
        'get the next file in the list (if any)
        LET file$ = WADs_Split$("")
    LOOP WHILE file$ <> ""
END IF

'-----------------------------------------------------------------------------
'[09] DEH & BEX:
'-----------------------------------------------------------------------------
'has a DEH file been specified?
LET CMD_DEH$ = STRGET$(Games_Selected.deh)

'does the IWAD provide its own DEH file?
'CHEX.WAD does this, so does REKKR stand-alone
IF IWADs_Selected.deh <> 0 THEN
    'TODO: handle conflict
    LET CMD_DEH$ = STRGET$(IWADs_Selected.deh)
	'if this is CHEX.WAD then PrBoom+ likes a `-exe chex` parameter for
	'demo compatibility. we don't have a generic way to sepecify this yet
	IF UCASE$(Paths_GetFileBaseName$(CMD_IWAD$)) = "CHEX" THEN
		LET CMD$ = CMD$ + " -exe chex"
	END IF
END IF

IF CMD_DEH$ <> "" THEN
    'find the DEH file
    'FIXME: file missing?
    LET CMD_DEH$ = WADs_Find$(CMD_DEH$)
    'add it to the command-line
    LET CMD$ = CMD$ + " -deh " _
             + CHR$(34) + Launch_FixPath$(CMD_DEH$) + CHR$(34)
    'and announce
    COLOR YELLOW: PRINT "         -deh : ";: COLOR UI_FORECOLOR
    PRINT RTRUNCATE$(Launch_ClipPath$(CMD_DEH$), UI_SCREEN_WIDTH - 17)
END IF

'has a BEX file been specified?
LET CMD_BEX$ = STRGET$(Games_Selected.bex)
IF CMD_BEX$ <> "" THEN
    'find the BEX file
    'FIXME: file missing?
    LET CMD_BEX$ = WADs_Find$(CMD_BEX$)
    'add it to the command-line
    LET CMD$ = CMD$ + " -bex " _
             + CHR$(34) + Launch_FixPath$(CMD_BEX$) + CHR$(34)
    'and announce
    COLOR YELLOW: PRINT "         -bex : ";: COLOR UI_FORECOLOR
    PRINT RTRUNCATE$(Launch_ClipPath$(CMD_BEX$), UI_SCREEN_WIDTH - 17)
END IF

'-----------------------------------------------------------------------------
'[10] demo playback:
'-----------------------------------------------------------------------------
IF LEN(CMD_DEMO$) <> 0 THEN
    'try and find the file:
    DIM demo$
    'try the folder from which the launcher was called
    LET demo$ = DIR_CUR$ + CMD_DEMO$
    'if it doesn't exist there...
    IF NOT _FILEEXISTS(demo$) THEN
        'try the special demos folder
        LET demo$ = DIR_DEMOS$ + CMD_DEMO$
        'if it doesn't exist there...
        IF NOT _FILEEXISTS(demo$) THEN
            'FIXME: handle demo file missing
        END IF
    END IF
    
    'add the command-line param to play the demo
    LET CMD$ = CMD$ + " -playdemo " _
             + CHR$(34) + Launch_FixPath$(demo$) + CHR$(34)
             
    COLOR YELLOW: PRINT "    -playdemo : ";: COLOR UI_FORECOLOR
    PRINT RTRUNCATE$(Launch_ClipPath$(demo$), UI_SCREEN_WIDTH - 17)
END IF

'-----------------------------------------------------------------------------
'[11] warp & skill:
'-----------------------------------------------------------------------------
'is a warp requested? (episode and/or map-number)
IF Games_Selected.warp_e >= 0 _
OR Games_Selected.warp_m >= 0 THEN
    'compose the -warp parameter for engines:
    DIM e$: LET e$ = STR$(Games_Selected.warp_e)
    DIM m$: LET m$ = STR$(Games_Selected.warp_m)
    DIM warp$
    
    IF Games_Selected.warp_e >= 0 THEN LET warp$ = warp$ + e$
    IF Games_Selected.warp_m >= 0 THEN LET warp$ = warp$ + m$
    
    'add to the command-line and announce
    LET CMD$ = CMD$ + " -warp" + warp$
    COLOR YELLOW: PRINT "        -warp :";: COLOR UI_FORECOLOR
    PRINT warp$
    
    'do we need to ask for a skill level?
    '(warp, but no skill-level provided)
    IF Games_Selected.skill < 0 THEN
        PRINT ""
        PRINT "--------------------------------------------------------------------------------"
        PRINT ""
        PRINT "         CHOOSE YOUR SKILL LEVEL:"
        PRINT "         (Press numbered key)"
        PRINT ""
        
        'get the list of skill-levels from the IWAD meta-data;
        'if there is none, default to DOOM's descriptions
        IF Games_Selected.iwad = 0 _
        OR IWADs_Selected.skill1 = 0 _
        THEN
            PRINT "         [1]  I'm Too Young To Die"
            PRINT "         [2]  Hey, Not Too Rough"
            PRINT "         [3]  Hurt Me Plenty"
            PRINT "         [4]  Ultra-Violence"
            PRINT "         [5]  Nightmare!"
        ELSE
            'work through the list of IWAD skill-levels (up to 9)
            IF IWADs_Selected.skill1 > 0 THEN
                PRINT "         [1]  ";
                PRINT STRGET$(IWADs_Selected.skill1)
            END IF
            IF IWADs_Selected.skill2 > 0 THEN
                PRINT "         [2]  ";
                PRINT STRGET$(IWADs_Selected.skill2)
            END IF
            IF IWADs_Selected.skill3 > 0 THEN
                PRINT "         [3]  ";
                PRINT STRGET$(IWADs_Selected.skill3)
            END IF
            IF IWADs_Selected.skill4 > 0 THEN
                PRINT "         [4]  ";
                PRINT STRGET$(IWADs_Selected.skill4)
            END IF
            IF IWADs_Selected.skill5 > 0 THEN
                PRINT "         [5]  ";
                PRINT STRGET$(IWADs_Selected.skill5)
            END IF
            IF IWADs_Selected.skill6 > 0 THEN
                PRINT "         [6]  ";
                PRINT STRGET$(IWADs_Selected.skill6)
            END IF
            IF IWADs_Selected.skill7 > 0 THEN
                PRINT "         [7]  ";
                PRINT STRGET$(IWADs_Selected.skill7)
            END IF
            IF IWADs_Selected.skill8 > 0 THEN
                PRINT "         [8]  ";
                PRINT STRGET$(IWADs_Selected.skill8)
            END IF
            IF IWADs_Selected.skill9 > 0 THEN
                PRINT "         [9]  ";
                PRINT STRGET$(IWADs_Selected.skill9)
            END IF
        END IF
        
        PRINT ""
        PRINT "         [?]";
        'position a blinking cursor over the question-mark
        LOCATE , POS(0)-2, 1, 0, 31
        
        DO
            LET key$ = INKEY$
        LOOP WHILE ISINT(key$) = FALSE
        
        'turn off the cursor
        LOCATE ,1 , 0
        
        PRINT "--------------------------------------------------------------------------------"
        PRINT ""
        
        LET Games_Selected.skill = VAL(key$)
    END IF
END IF

IF Games_Selected.skill >= 0 THEN
    LET CMD$ = CMD$ + " -skill" + STR$(Games_Selected.skill)
    COLOR YELLOW: PRINT "       -skill :";: COLOR UI_FORECOLOR
    PRINT STR$(Games_Selected.skill)
END IF

'-----------------------------------------------------------------------------
'[12] execute scripts:
'-----------------------------------------------------------------------------
IF CMD_EXEC$ <> "" THEN
    'try and find the file:
    DIM exec$
    'try the folder from which the launcher was called
    LET exec$ = DIR_CUR$ + CMD_EXEC$
    'if it doesn't exist there...
    IF NOT _FILEEXISTS(demo$) THEN
        'TODO: check engine folder? check config folder?
        'FIXME: handle exec file missing
    END IF
    
    'add the command-line param to play the demo
    LET CMD$ = CMD$ + " -exec " _
             + CHR$(34) + Launch_FixPath$(exec$) + CHR$(34)
             
    COLOR YELLOW: PRINT "        -exec : ";: COLOR UI_FORECOLOR
    PRINT RTRUNCATE$(Launch_ClipPath$(exec$), UI_SCREEN_WIDTH - 17)
END IF

'-----------------------------------------------------------------------------
'[13] resolution & full-screen:
'-----------------------------------------------------------------------------
'the full-screen parameter varies by engine-type;
IF Engines_Selected.kin = KIN_Z THEN
    'ZDoom-based engines use `+fullscreen`
    LET CMD$ = CMD$ + " +fullscreen"
ELSE
    'everything else `-fullscreen`
    LET CMD$ = CMD$ + " -fullscreen"
END IF

'a current bug/limitation of Choco-* engines in v3.0 is that
'specifying the screen size forces fullscreen off
IF Engines_Selected.kin <> KIN_V THEN
    DIM screen_width AS LONG
    LET screen_width = _DESKTOPWIDTH
    DIM screen_height AS LONG
    LET screen_height = _DESKTOPHEIGHT

    LET CMD$ = CMD$ + " -width " + STRINT$(screen_width)
    LET CMD$ = CMD$ + " -height " + STRINT$(screen_height)
    
    COLOR YELLOW: PRINT "       -width :";
    COLOR UI_FORECOLOR: PRINT STR$(screen_width);
    COLOR YELLOW: PRINT "  -height :";
    COLOR UI_FORECOLOR: PRINT STR$(screen_height)
END IF

'-----------------------------------------------------------------------------
'[14] launch!
'-----------------------------------------------------------------------------
'specify the save-game directory as the 'current' directory
'TODO: a more specific way of detecting this?
IF LEFT$(STRGET$(Engines_Selected.engine), 11) = "prboom-plus" THEN
    LET CMD$ = CMD$ + " -save " + CHR$(34) + "." + CHR$(34)
ELSE
    'all other engines use `-savedir`,
    'except doom64x which has no support yet
    LET CMD$ = CMD$ + " -savedir " + CHR$(34) + "." + CHR$(34)
END IF

'where screenshots get saved varies by engine; many put them in the engine's
'folder regardless of the 'current' directory and not all support changing the
'directory. to the best of our ability, try to put them in the "saves/<engine>"
'folder; one up from the 'current' directory used for save-games. we specify
'the path fully (and not relative) in case different engines interpret the
'relative path as from different starting directories to each other
DIM DIR_SCREENSHOT$
LET DIR_SCREENSHOT$ = Paths_StripSlash$(DIR_EXE$ + DIR_SAVE_ENGINE$)
LET CMD$ = CMD$ + " -shotdir " + CHR$(34) + DIR_SCREENSHOT$ + CHR$(34)

'DOOM 64 EX: fix for Sound Font not being found (it's looking in the 'current
'directory' rather than the executable's directory?)
IF STRGET$(Engines_Selected.engine) = "doom64ex" THEN
    LET CMD$ = CMD$ + " -setvars s_soundfont " + CHR$(34) _
                    + Launch_FixPath$(DIR_ENGINE$ + "DOOMSND.SF2") + CHR$(34)
END IF

'any engine-specific command-line params to be added?
IF Engines_Selected.cmd <> 0 THEN
    LET CMD$ = CMD$ + " " + STRGET$(Engines_Selected.cmd)
END IF

'auto-quit? (ZDoom-based engines only)
IF CMD_QUIT` THEN
    LET CMD$ = CMD$ + " +quit"
    COLOR YELLOW: PRINT "        +quit": COLOR UI_FORECOLOR
END IF

COLOR WHITE
PRINT ""
PRINT "   GET PSYCHED!"
PRINT ""

IF CMD_DEBUG` THEN
    PRINT CMD$
    PRINT ""
    PRINT ""
    
    VIEW PRINT
    
    LOCATE UI_SCREEN_HEIGHT, 1
    COLOR BLACK+BLINK, WHITE
    
    DIM w%
    LET w% = LEN("PRESS ANY KEY!")
    PRINT SPACE$((UI_SCREEN_WIDTH - w%) / 2) _
        + "PRESS ANY KEY!" _
        + SPACE$((UI_SCREEN_WIDTH - w%) / 2);
    
    COLOR WHITE, BLACK
    SLEEP
END IF

'set the directory for the game engine to assume as default; note that all
'paths on the command-line must be relative to the save-game folder!
CHDIR DIR_EXE$ + DIR_SAVE_GAME$

'has the `/WAIT` command-line option been specified?
IF CMD_WAIT` THEN
    'start the engine and wait for it to close
    SHELL CMD$
ELSE
    'start the engine, but continue running here
    SHELL _DONTWAIT CMD$
END IF

SYSTEM

'=============================================================================

errMkDir:
PRINT ""
PRINT "ERROR: could not create required folder!"
PRINT ""
PRINT "Press Any Key to Exit"
SLEEP: SYSTEM 1
    
err_fileMissing:
SYSTEM 1

'make a folder if it doesn't exist; capture errors (e.g. read-only)
'=============================================================================
SUB Launch_MkDir(path$)
    ON ERROR GOTO errMkDir
    
    IF NOT _DIREXISTS(path$) THEN
        'folder is missing? attempt to make it, but be aware
        'of that potentially failing (read-only media?)
        MKDIR (path$)
    END IF
    
    'reset the error handler
    ON ERROR GOTO 0
END SUB

'correct a relative path from the save-game folder
'=============================================================================
FUNCTION Launch_FixPath$(path$)
    'is the path relative?
    IF NOT Paths_IsAbsolute(path$) THEN
        LET Launch_FixPath$ = FIX_PATH$ + path$
    ELSE
        'if the path is a sub-folder of PortaDOOM, convert it to relative
        IF LEFT$(path$, LEN(DIR_EXE$)) = DIR_EXE$ THEN
            LET Launch_FixPath$ = FIX_PATH$ + MID$(path$, LEN(DIR_EXE$) + 1)
        ELSE
            LET Launch_FixPath$ = path$
        END IF
    END IF
END FUNCTION

'if an absolute path is within PortaDOOM's folders, make it relative
'=============================================================================
FUNCTION Launch_ClipPath$(path$)
    IF LEFT$(path$, LEN(DIR_EXE$)) = DIR_EXE$ THEN
        LET Launch_ClipPath$ = MID$(path$, LEN(DIR_EXE$) + 1)
    ELSE
        LET Launch_ClipPath$ = path$
    END IF
END FUNCTION
